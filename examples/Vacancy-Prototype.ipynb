{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "WARNING: Method definition phi(Any) in module Main at In[1]:3 overwritten at In[5]:3.\n",
      "WARNING: Method definition dphi(Any) in module Main at In[1]:4 overwritten at In[5]:4.\n",
      "WARNING: Method definition ddphi(Any) in module Main at In[1]:5 overwritten at In[5]:5.\n",
      "WARNING: Method definition pphi(Any) in module Main at In[1]:6 overwritten at In[5]:6.\n",
      "WARNING: Method definition energy(Array{T<:Any, 2}) in module Main at In[1]:9 overwritten at In[5]:9.\n",
      "WARNING: Method definition dofs2pos(Array{T<:Any, 1}) in module Main at In[1]:18 overwritten at In[5]:18.\n",
      "WARNING: Method definition pos2dofs(Array{T<:Any, 2}) in module Main at In[1]:19 overwritten at In[5]:19.\n",
      "WARNING: Method definition energy(Array{T<:Any, 1}) in module Main at In[1]:21 overwritten at In[5]:21.\n",
      "WARNING: Method definition gradient(Array{T<:Any, 2}) in module Main at In[1]:24 overwritten at In[5]:24.\n",
      "WARNING: Method definition gradient(Array{T<:Any, 1}) in module Main at In[1]:36 overwritten at In[5]:36.\n",
      "WARNING: Method definition exp_precond(Any) in module Main at In[1]:39 overwritten at In[5]:39.\n",
      "WARNING: Method definition #exp_precond(Array{Any, 1}, Main.#exp_precond, Any) in module Main overwritten.\n",
      "WARNING: Method definition refconfig() in module Main at In[1]:59 overwritten at In[5]:59.\n",
      "WARNING: Method definition #refconfig(Array{Any, 1}, Main.#refconfig) in module Main overwritten.\n",
      "WARNING: Method definition test_gradient() in module Main at In[1]:84 overwritten at In[5]:84.\n",
      "WARNING: Method definition sd() in module Main at In[1]:104 overwritten at In[5]:104.\n",
      "WARNING: Method definition #sd(Array{Any, 1}, Main.#sd) in module Main overwritten.\n",
      "WARNING: Method definition bb() in module Main at In[1]:123 overwritten at In[5]:123.\n",
      "WARNING: Method definition #bb(Array{Any, 1}, Main.#bb) in module Main overwritten.\n",
      "WARNING: Method definition dimer() in module Main at In[1]:157 overwritten at In[5]:157.\n",
      "WARNING: Method definition #dimer(Array{Any, 1}, Main.#dimer) in module Main overwritten.\n",
      "WARNING: Method definition bbdimer() in module Main at In[1]:194 overwritten at In[5]:194.\n",
      "WARNING: Method definition #bbdimer(Array{Any, 1}, Main.#bbdimer) in module Main overwritten.\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "bbdimer (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using ForwardDiff, PyPlot\n",
    "\n",
    "phi(r) = r^(-12) - 2 * r^(-6)\n",
    "dphi(r) = -12 * r^(-13) + 12 * r^(-7)\n",
    "ddphi(r) = 12*13 * r^(-14) - 7*12 * r^(-8)\n",
    "pphi(r) = (12*13 - 7*12) * r^(-8)\n",
    "\n",
    "function energy(X::Matrix)\n",
    "    E = 0.0 \n",
    "    nX = size(X,2)\n",
    "    for i = 1:nX-1, j = i+1:nX\n",
    "        rij = norm(X[:,i] - X[:,j])\n",
    "        E += phi(rij)\n",
    "    end \n",
    "    return E\n",
    "end \n",
    "\n",
    "dofs2pos(x::Vector) = reshape(x, 2, length(x) ÷ 2) \n",
    "pos2dofs(X::Matrix) = X[:]\n",
    "\n",
    "energy(x::Vector) = energy(dofs2pos(x))\n",
    "\n",
    "function gradient(X::Matrix)\n",
    "    dE = zeros(size(X))\n",
    "    nX = size(X,2)\n",
    "    for i = 1:nX-1, j = i+1:nX \n",
    "        Rij = X[:,i] - X[:,j]\n",
    "        rij = norm(Rij)\n",
    "        a = dphi(rij) * Rij / rij \n",
    "        dE[:,i] += a \n",
    "        dE[:,j] -= a \n",
    "    end \n",
    "    return dE\n",
    "end \n",
    "\n",
    "gradient(x::Vector) = pos2dofs(gradient(dofs2pos(x)))\n",
    "\n",
    "function exp_precond(x; rcut = 2.5, α=3.0)\n",
    "   X = dofs2pos(x)\n",
    "   I = dofs2pos(1:length(x) |> collect)\n",
    "   nX = size(X, 2)\n",
    "   P = zeros(2*nX, 2*nX)\n",
    "   for i = 1:nX, j = i+1:nX\n",
    "      Ii = I[:,i]; Ij = I[:,j]\n",
    "      Rij = X[:,i] - X[:,j]\n",
    "      rij = norm(Rij)\n",
    "      if 0 < rij < rcut\n",
    "         a = pphi(rij) * eye(2)\n",
    "         P[Ii, Ij] -= a\n",
    "         P[Ij, Ii] -= a\n",
    "         P[Ii, Ii] += a\n",
    "         P[Ij, Ij] += a\n",
    "      end\n",
    "   end\n",
    "   return sparse(P) + 0.001 * speye(2*nX)\n",
    "end\n",
    "\n",
    "function refconfig(; R = 4.1, case=:min, rattle=0.01)\n",
    "   A = [1.0 cos(π/3); 0.0 sin(π/3)]\n",
    "   cR = ceil(Int, R / minimum(svd(A)[2]))\n",
    "   t = collect(-cR:cR)\n",
    "   x = ones(length(t)) * t'\n",
    "   y = t * ones(length(t))'\n",
    "   X = A * [x[:] y[:]]'\n",
    "   r = sqrt(sumabs2(X, 1))\n",
    "   Xref = X[:, find(0 .< r .<= R)]\n",
    "   r = sqrt(sumabs2(Xref, 1))\n",
    "   I0 = find(r .<= 1.1)[1]\n",
    "   if I0 != 1\n",
    "      Xref[:, [1,I0]] = Xref[:, [I0,1]]\n",
    "      r[[1,I0]] = r[[I0,1]]\n",
    "   end\n",
    "    \n",
    "   if case == :min \n",
    "        Xref[:, 1] *= 0.9 \n",
    "    elseif case == :saddle\n",
    "        Xref[:, 1] *= 0.6 \n",
    "    end \n",
    "    Xref += 2 * rattle * (rand(size(Xref)) - 0.5)\n",
    "   return Xref\n",
    "end\n",
    "\n",
    "function test_gradient()\n",
    "    x = refconfig(3.1) |> pos2dofs\n",
    "    N = length(x)\n",
    "    x += 0.01 * rand(N)\n",
    "    E = energy(x)\n",
    "    dE = gradient(x)\n",
    "    dEh = [] \n",
    "    for p = 3:10\n",
    "        h = 0.1^p\n",
    "        dEh = zeros(N)\n",
    "        for i = 1:N\n",
    "            x[i] += h\n",
    "            dEh[i] = (energy(x) - E) / h\n",
    "            x[i] -= h\n",
    "        end\n",
    "        @show norm(dE - dEh, Inf)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "function sd(; R = 3.1, precond = x -> speye(length(x)), α = 0.001, TOL = 1e-5, maxnit = 1000, verbose=false)\n",
    "    x = refconfig(R=R, rattle=0.0) |> pos2dofs \n",
    "    for n = 1:maxnit \n",
    "        g = gradient(x)\n",
    "        if verbose\n",
    "            @printf(\" %d  |  %4.2e  \\n\", n, norm(g, Inf))\n",
    "        end \n",
    "        if norm(g, Inf) <= TOL\n",
    "            println(\"success after $n iterations\")\n",
    "            return x\n",
    "        end\n",
    "        x = x - α * (precond(x) \\ g)\n",
    "    end \n",
    "    println(\"sd did not converge in $maxnit iterations\")\n",
    "    return x\n",
    "end\n",
    "\n",
    "\n",
    "function bb(; R = 3.1, precond = x -> speye(length(x)), α = 0.001, TOL = 1e-5, maxnit = 1000, verbose=false)\n",
    "\n",
    "    x = refconfig(R=R, rattle=0.0) |> pos2dofs    \n",
    "    \n",
    "    g = gradient(x) \n",
    "    gP = precond(x) \\ g\n",
    "    xold = x\n",
    "    x = xold - α * gP\n",
    "    \n",
    "    for n = 1:maxnit \n",
    "        gold = g\n",
    "        gPold = gP\n",
    "        g = gradient(x)\n",
    "        \n",
    "        if verbose\n",
    "            @printf(\" %d  |  %4.2e  \\n\", n, norm(g, Inf))\n",
    "        end \n",
    "        if norm(g, Inf) <= TOL\n",
    "            println(\"success after $n iterations\")\n",
    "            return x\n",
    "        end\n",
    "        \n",
    "        gP = precond(x) \\ g\n",
    "        α = dot(x - xold, g - gold) / dot(g - gold, gP - gPold)\n",
    "\n",
    "        xold = x \n",
    "        x = xold - α * gP\n",
    "    end \n",
    "    println(\"sd did not converge in $maxnit iterations\")\n",
    "    return x\n",
    "end\n",
    "\n",
    "\n",
    "function dimer(; R = 3.1, precond = x -> speye(length(x)), \n",
    "                 α = 0.001, β = 0.001, TOL = 1e-5, TOLr = 1e-3, \n",
    "                 maxnit = 2000, verbose=false)\n",
    "    x = refconfig(R=R, case=:saddle, rattle=0.0) |> pos2dofs \n",
    "    v = zeros(x)\n",
    "    v[1:2] = - x[1:2]\n",
    "    l = 1e-3\n",
    "    \n",
    "    for n = 1:maxnit\n",
    "        P = precond(x)\n",
    "        v /= sqrt(dot(v, P*v))\n",
    "        gp = gradient(x + l * v)\n",
    "        gm = gradient(x - l * v)\n",
    "        gavg = 0.5 * (gp + gm)\n",
    "        Hxv = (gp - gm) / (2*l)\n",
    "\n",
    "        res_t = norm(gavg, Inf)\n",
    "        res_r = norm(Hxv - dot(v, Hxv) * (P * v), Inf)\n",
    "        \n",
    "        if verbose\n",
    "            @printf(\" %d  |  %4.2e   %4.2e \\n\", n, res_t, res_r)\n",
    "        end \n",
    "        \n",
    "        if res_t <= TOL && res_r <= TOLr\n",
    "            println(\"success after $n iterations\")\n",
    "            return x, v\n",
    "        end\n",
    "        \n",
    "        x = x - α * (P \\ gavg - 2 * dot(v, gavg) * v)\n",
    "        v = v - β * (P \\ Hxv - dot(v, Hxv) * v)\n",
    "    end \n",
    "    println(\"dimer did not converge in $maxnit iterations\")\n",
    "    return x, v\n",
    "end\n",
    "\n",
    "\n",
    "function bbdimer(; R = 3.1, precond = x -> speye(length(x)), \n",
    "                 α = 0.001, β = 0.001, TOL = 1e-5, TOLr = 1e-3, \n",
    "                 maxnit = 2000, verbose=false)\n",
    "\n",
    "    x = refconfig(R=R, case=:saddle, rattle=0.0) |> pos2dofs \n",
    "    v = zeros(x)\n",
    "    v[1:2] = - x[1:2]\n",
    "    l = 1e-3\n",
    "        \n",
    "    P = precond(x)\n",
    "    v /= sqrt(dot(v, P*v))\n",
    "    gp = gradient(x + l * v)\n",
    "    gm = gradient(x - l * v)\n",
    "    gavg = 0.5 * (gp + gm)\n",
    "    Hxv = (gp - gm) / (2*l)\n",
    "    Ft = gavg - 2 * dot(v, gavg) * (P * v)\n",
    "    Fr =  Hxv - dot(v, Hxv) * (P * v)\n",
    "    FtP = P \\ gavg - 2 * dot(v, gavg) * v\n",
    "    FrP = P \\ Hxv - dot(v, Hxv) * v\n",
    "\n",
    "    x_old, v_old = x, v\n",
    "    x = x_old - α * FtP\n",
    "    v = v_old - β * FrP\n",
    "\n",
    "    for nit = 1:maxnit \n",
    "\n",
    "        # store previous state\n",
    "        Ft_old, Fr_old, FtP_old, FrP_old = Ft, Fr, FtP, FrP \n",
    "        \n",
    "        # update quantities \n",
    "        P = precond(x)\n",
    "        v /= sqrt(dot(v, P*v))\n",
    "        gp = gradient(x + l * v)\n",
    "        gm = gradient(x - l * v)\n",
    "        gavg = 0.5 * (gp + gm)\n",
    "        Hxv = (gp - gm) / (2*l)\n",
    "        λ = dot(Hxv, v)\n",
    "        Ft = gavg - 2 * dot(v, gavg) * (P * v)\n",
    "        Fr =  Hxv - λ * (P * v)\n",
    "        \n",
    "        rest = norm(Ft, Inf) \n",
    "        resr = norm(Fr, Inf)\n",
    "        \n",
    "        if verbose\n",
    "            @printf(\"%4d | %1.2e  %1.2e  %1.2e  %1.2e  %1.2e \\n\",\n",
    "               nit, rest, resr, λ, α, β)        \n",
    "        end \n",
    "        if rest <= TOL && resr <= TOLr \n",
    "            println(\"success after $nit iterations\")\n",
    "            return x, v\n",
    "        end\n",
    "        \n",
    "        FtP = P \\ gavg - 2 * dot(v, gavg) * v\n",
    "        FrP = P \\ Hxv - dot(v, Hxv) * v\n",
    "        \n",
    "        α = dot(x - x_old, Ft - Ft_old) / dot(FtP - FtP_old, Ft - Ft_old)\n",
    "        β = dot(v - v_old, Fr - Fr_old) / dot(FrP - FrP_old, Fr - Fr_old)\n",
    "        \n",
    "        x_old, v_old = x, v\n",
    "        x = x_old - α * FtP \n",
    "        v = v_old - β * FrP\n",
    "    end\n",
    "    println(\"dimer did not converge in $maxnit iterations\") \n",
    "    return x, v\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "MINIMISATION\n",
      "R = 3.1\n",
      "SD-I: success after 309 iterations\n",
      "SD-P: success after 98 iterations\n",
      "BB-I: success after 55 iterations\n",
      "BB-P: success after 24 iterations\n",
      "R = 4.1\n",
      "SD-I: success after 364 iterations\n",
      "SD-P: success after 89 iterations\n",
      "BB-I: success after 62 iterations\n",
      "BB-P: success after 24 iterations\n",
      "R = 5.1\n",
      "SD-I: success after 432 iterations\n",
      "SD-P: success after 85 iterations\n",
      "BB-I: success after 81 iterations\n",
      "BB-P: success after 24 iterations\n",
      "R = 7.1\n",
      "SD-I: success after 615 iterations\n",
      "SD-P: success after 83 iterations\n",
      "BB-I: success after 87 iterations\n",
      "BB-P: success after 23 iterations\n"
     ]
    }
   ],
   "source": [
    "println(\"MINIMISATION\")\n",
    "for R in (3.1, 4.1, 5.1, 7.1) # , 10.1)\n",
    "    println(\"R = $R\")\n",
    "    print(\"SD-I: \")\n",
    "    xI = sd(R = R, α=0.003, verbose = false);\n",
    "    print(\"SD-P: \")\n",
    "    xP = sd(R = R, precond = exp_precond, α = 1.0, verbose = false);\n",
    "    print(\"BB-I: \")\n",
    "    xIbb = bb(R = R, α=0.001, verbose = false);\n",
    "    print(\"BB-P: \")\n",
    "    xPbb = bb(R = R, α=0.1, verbose = false, precond = exp_precond);\n",
    "end "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "SADDLE SEARCH\n",
      "R = 3.1\n",
      "  DIMER-I: success after 650 iterations\n",
      "  DIMER-P: success after 114 iterations\n",
      "BBDIMER-I: success after 76 iterations\n",
      "BBDIMER-P: success after 31 iterations\n",
      "R = 4.1\n",
      "  DIMER-I: success after 852 iterations\n",
      "  DIMER-P: success after 107 iterations\n",
      "BBDIMER-I: success after 69 iterations\n",
      "BBDIMER-P: success after 29 iterations\n",
      "R = 5.1\n",
      "  DIMER-I: success after 1133 iterations\n",
      "  DIMER-P: success after 106 iterations\n",
      "BBDIMER-I: success after 96 iterations\n",
      "BBDIMER-P: success after 27 iterations\n",
      "R = 7.1\n",
      "  DIMER-I: success after 1958 iterations\n",
      "  DIMER-P: success after 105 iterations\n",
      "BBDIMER-I: success after 175 iterations\n",
      "BBDIMER-P: success after 29 iterations\n"
     ]
    }
   ],
   "source": [
    "println(\"SADDLE SEARCH\")\n",
    "for R in (3.1, 4.1, 5.1, 7.1)  # , 10.1)\n",
    "    println(\"R = $R\")\n",
    "    print(\"  DIMER-I: \")\n",
    "    xI = dimer(; R=R, verbose=false, α=0.0015, β = 0.001);\n",
    "    print(\"  DIMER-P: \")\n",
    "    xP = dimer(; R=R, verbose=false, α=0.9, β = 0.3, precond=exp_precond);\n",
    "    print(\"BBDIMER-I: \")\n",
    "    xIbb = bbdimer(R = R, α=0.001, β=0.001, verbose = false);\n",
    "    print(\"BBDIMER-P: \")\n",
    "    xPbb = bbdimer(R = R, α=0.1, β=0.1, verbose = false, precond = exp_precond);\n",
    "end "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "anaconda-cloud": {},
  "kernelspec": {
   "display_name": "Julia 0.5.1-pre",
   "language": "julia",
   "name": "julia-0.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.5.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
